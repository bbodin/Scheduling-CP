{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Model Description\n",
    "\n",
    "## Parameters\n",
    "\n",
    "Each paramter is matched to its code equivalent as follow. Parameters are declared either as a range or enum type, and can be imported into MiniZinc using dzn files. Qualities of these parameters/enum types are declared as arrays. Range in the following code starts from 1. Note that the CP model ignores PE $\\rightarrow$ router links by taking routers as NoC nodes. Adjustments are made to starting and ending times to acommodate this setting.\n",
    "\n",
    "<dl>\n",
    "    <dt>Time is declared with a max (range) b:</dt> \n",
    "        <dd>$\\mathcal{T} = \\{t_1, \\dots, t_b\\}$</dd>\n",
    "        <code>int: max_time = b;</code> \n",
    "</dl>\n",
    "<dl>\n",
    "    <dt>Dataflow actors as enum type:</dt>\n",
    "        <dd>$\\mathcal{A} = \\{a_1, \\dots, a_m\\}$</dd>\n",
    "        <code>enum actors = {a1, a2, ..., am};</code>\n",
    "</dl>\n",
    "<dl>\n",
    "    <dt>Execution time of actors:</dt>\n",
    "        <dd>$\\mathcal{E} = \\{e_1, \\dots. e_n\\}$</dd>\n",
    "        <code>array[actors] of int: execution_time = [0,0,0];</code>\n",
    "</dl>\n",
    "<dl>\n",
    "    <dt>NoC size is declared with a max int such that $n*n=m$</dt>\n",
    "        <dd>$\\mathcal{V} = \\{v_1, \\dots, v_m\\}$</dd>\n",
    "        <code>int: noc = n;</code>\n",
    "</dl>\n",
    "<dl>\n",
    "    <dt>Messages list declared with enum type</dt>\n",
    "        <dd>$\\mathcal{M} = \\{m_1, \\dots. m_n\\}$</dd>\n",
    "        <code>enum messages = {m1, m2, ..., mn};</code>\n",
    "</dl>\n",
    "<dl>\n",
    "    <dt>TDM max slot and TDM table for router to router as 2d (flattened) array</dt>\n",
    "    <dd>$\\mathcal{NOC_X} = \\{1, \\dots, x\\}$</dd>\n",
    "    <dd>$\\mathcal{NOC_Y} = \\{1, \\dots, y\\}$</dd>\n",
    "    <dd>$TDM^{max} \\in \\mathbb{Z}$</dd>\n",
    "    <dd>$TDM_{(x \\in \\mathcal{NOC_X}, y \\mathcal{NOC_Y})} \\in \\mathbb{Z}$</dd>\n",
    "    <dd>Each element in the tdma table represents the tdma slot for source to destimation node, and can be called with tdma_table[actor1, actor2]</dd>\n",
    "    <code>int: tdma_max = 2;</code>\n",
    "    <code>array[1..noc*noc, 1..noc*noc] of int: tdma_table = array2d(1..noc, 1..noc, [1, ..., 1,\n",
    "                                                                                          ...,    \n",
    "                                                                                      1, ..., 1]);</code>\n",
    "\n",
    "<dl>\n",
    "    <dt>Source and destination of messages</dt>\n",
    "        <dd>$src_M(m_i) = a_s \\in \\mathcal{A}$</dd>\n",
    "        <dd>$dest_M(m_i) = a_d \\in \\mathcal{A}$</dd>\n",
    "        <code>array[messages] of int: src = [1,2,3,3,3]; \n",
    "array[messages] of int: dst = [2,3,1,1,1]; \n",
    "function actors: source(messages: m) = actors[src[m]];\n",
    "function actors: dest(messages: m) = actors[dst[m]];</code>\n",
    "</dl>\n",
    "<dl>\n",
    "    <dt>Message dependencies</dt>\n",
    "    <dd>Elements of the array are the indexes in the message list where 0 means Null (can start whenever)</dd>\n",
    "        <code>array[messages] of int: dependencies = [0,1,2,3,4];</code>\n",
    "</dl>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Variables and Functions\n",
    "\n",
    "Variables are defined as arrays in relationship to the above enum types. We may use the constraint condition in code to define these relationships.\n",
    "<dl>\n",
    "    <dt>Message Mapping</dt>\n",
    "        <dd>$map_x(a) = x_1 + 2x_2 + \\dots +|NoC_x|\\times x_{|NoC_x|} \\quad | \\quad \\sum_i x_{ai} = 1$</dd>\n",
    "        <dd>$map_y(a) = y_1 + 2y_2 + \\dots +|NoC_y|\\times y_{|NoC_y|} \\quad | \\quad \\sum_i x_{ai} = 1$</dd>\n",
    "    <dd>The mapppings in code are identified uniquely by their index in the 2d array</dd>\n",
    "        <code>array[actors] of var 1..noc: mapx;\n",
    "array[actors] of var 1..noc: mapy;\n",
    "function var int: mapping(var int: x, var int: y) = (y-1)*noc + x;</code>\n",
    "</dl>\n",
    "<dl>\n",
    "    <dt>Message Routing</dt>\n",
    "    <dd>$\\begin{align}route_d(m) &= \\{d_1^m, \\dots, d_b^m\\} \\text{ where }\n",
    "        d_i^m = \n",
    "            \\begin{cases}\n",
    "                1 \\text{ if movement in direction $d$ at time $i$}\\\\\n",
    "                0 \\text{ else } \n",
    "            \\end{cases}\n",
    "            \\label{eqr}\n",
    "    \\end{align}$</dd>\n",
    "    <code>array[messages, 1..max_time] of var 0..1: route_n;\n",
    "array[messages, 1..max_time] of var 0..1: route_s;\n",
    "array[messages, 1..max_time] of var 0..1: route_e;\n",
    "array[messages, 1..max_time] of var 0..1: route_w;</code>\n",
    "</dl>\n",
    "<dl>\n",
    "    <dt>Message Start and End Time (Route length)</dt>\n",
    "    <dd>$fire(m) = t \\in \\mathcal{T} \\quad | \\quad \\sum_{i=1}^{i=t+1} \\sum_d d^m_i = 1$</dd>\n",
    "    <dd>$|route(m)| = \\sum_{t=t1}^{t=b} \\sum_{d=\\{N,S,E,W\\}} d_t^m\\label{eql}$</dd>\n",
    "</dl>\n",
    "\n",
    "`array[messages] of var 1..max_time: starts;\n",
    "array[messages] of var 1..max_time: ends;\n",
    "constraint forall (m in messages)(\n",
    "  starts[m] = min(t in 1..max_time where route_n[m,t]+route_s[m,t]+route_e[m,t]+route_w[m,t] > 0)(t)\n",
    ");\n",
    "constraint forall (m in messages)(\n",
    "  ends[m] = max(t in 1..max_time where route_n[m,t]+route_s[m,t]+route_e[m,t]+route_w[m,t] > 0)(t)\n",
    ");`\n",
    "\n",
    "<dl>\n",
    "    <dt>Schedule Makespan</dt>\n",
    "    <dd>$S_{max}$</dd>\n",
    "    <code>var 1..max_time: makespan;</code>\n",
    "</dl>\n",
    "\n",
    "## Objective\n",
    "\n",
    "$\\min S_{max}$\n",
    "\n",
    "<code>solve minimize makespan;</code>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Constraints\n",
    "\n",
    "1. The first constraint guarantees that a message sent from its source actor will reach its destination.\n",
    "$$\n",
    "\\begin{align}\n",
    "    &\\forall m \\in \\mathcal{M}:\\\\\n",
    "    &\\sum_{t=1}^{t=b} e^m_t - w^m_t = map_x(dest_M(m)) - map_x(src_M(m)) \\\\\n",
    "    &\\sum_{t=1}^{t=b} s^m_t - n^m_t = map_y(dest_M(m)) - map_y(src_M(m))\n",
    "\\end{align}\n",
    "$$\n",
    "\n",
    "`constraint forall(m in messages)(\n",
    "  sum([route_s[m,t] - route_n[m,t] | t in 1..max_time]) = mapy[dest(m)] - mapy[source(m)]\n",
    ");\n",
    "constraint forall(m in messages)(\n",
    "  sum([route_e[m,t] - route_w[m,t] | t in 1..max_time]) = mapx[dest(m)] - mapx[source(m)]\n",
    ");`"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "2. The second constraint guarantees the validity of the route variable where messages move one unit axis at a time and the route does not go off the NoC grid.\n",
    "$$\n",
    "\\begin{align}\n",
    "    \\sum_d d_t^m &\\leq 1 \\quad \\forall t \\in \\mathcal{T}, m \\in \\mathcal{M}\\label{c2}\\\\\n",
    "    \\sum_{t=t1}^{t=i} d0^m_t - d1^m_t &< |NoC_d| \\quad \\forall i \\leq b, (d0,d1) \\in \\{(N,S), (E,W)\\}\\label{c3}\n",
    "\\end{align}\n",
    "$$\n",
    "\n",
    "`constraint forall (m in messages)(\n",
    "  forall (t in 1..max_time)(\n",
    "    route_n[m,t]+route_s[m,t]+route_e[m,t]+route_w[m,t] <= 1\n",
    "  )\n",
    ");\n",
    "constraint forall(m in messages)(\n",
    "  forall(curr_t in 1..max_time)(\n",
    "    sum([route_s[m,t] - route_n[m,t] | t in 1..curr_t]) + mapy[source(m)] > 0 /\\ sum([route_s[m,t] - route_n[m,t] | t in 1..curr_t]) + mapy[source(m)] <= noc\n",
    "  )\n",
    ");\n",
    "constraint forall(m in messages)(\n",
    "  forall(curr_t in 1..max_time)(\n",
    "    sum([route_e[m,t] - route_w[m,t] | t in 1..curr_t]) + mapx[source(m)] > 0 /\\ sum([route_e[m,t] - route_w[m,t] | t in 1..curr_t]) + mapx[source(m)] <= noc \n",
    "  )\n",
    ");\n",
    "`\n",
    "    "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "3. The third constraint is relevant for bufferless NoC routing where messages can not be stored mid-traffic before being received by destination PEs. The following restriction prevents cases where a column of zeroes exist between columns of non-zeroes. In code, this is done with a triple for loop.\n",
    "$$\n",
    "\\begin{align}\n",
    "    \\sum_d d_{t=i}^m - \\sum_d d_{t=i+j}^m + \\sum_d d_{t=i+k}^m \\leq 1 \\quad &\\forall i \\leq b, i+k \\leq b, 0 < j < k,\\\\\n",
    "    & \\forall m \\in \\mathcal{M}\n",
    "\\end{align}\n",
    "$$\n",
    "\n",
    "`constraint forall (m in messages)(\n",
    "  forall (t in 1..max_time)(\n",
    "    forall(t1 in 1..max_time where t1 > t)(\n",
    "      forall(t2 in 1..max_time where t2 > t1)(\n",
    "        route_n[m,t]+route_s[m,t]+route_e[m,t]+route_w[m,t] - route_n[m,t1]+route_s[m,t1]+route_e[m,t1]+route_w[m,t1] + route_n[m,t2]+route_s[m,t2]+route_e[m,t2]+route_w[m,t2] <= 1\n",
    "      )\n",
    "    )\n",
    "  )\n",
    ");`\n",
    "    "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "4. The fourth constraint defines that an actor cannot have multiple messages sent at the same time.\n",
    "    $$\n",
    "\\begin{align}\n",
    "    \\sum_m \\sum^{t=i}_{t=1} \\sum_d d^m_t \\leq 1 \\quad \\forall t \\in \\mathcal{T}, \\forall a \\in \\mathcal{A} \\text{ such that }m \\in src_M(a) \n",
    "\\end{align}\n",
    "$$\n",
    "\n",
    "`constraint forall (a in actors)(\n",
    "  alldifferent([starts[m] | m in messages where source(m) = a])\n",
    ");`\n",
    "    "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "5. The fifth constraint defines $S_{max}$ to be larger than the sum of any message starting time, with the corresponding message $m$'s route length, execution time, and tdm delay. For static routing algorithms, the route length is the Manhattan distance between the source and destination actor. For dynamic routing, we make use of the $route_x, route_y$ values as described in constraint 6.\n",
    "    $$\n",
    "\\begin{align}\n",
    "    S_{max} \\geq t + |route(m)| \\quad  \\forall t \\in \\mathcal{T}, fire(m) = t\n",
    "\\end{align}\n",
    "$$\n",
    "\n",
    "`constraint forall (m in messages)(\n",
    "  makespan >= ends[m]\n",
    ");`\n",
    "    "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "6. The sixth constraint defines each subsequent message sent from $a$ to start later than the any of the sum of start time of messages delivered to $a$, route length, execution time, and tdma restrictions. This is replaced in code by the condition of having messages that depend on other messages be sent only after their end times\n",
    "\\begin{align}\n",
    "    t_j \\geq t_i + |route(m_i)| + e_i \\quad  &\\forall t_i,t_j \\in \\mathcal{T} \\quad |\\quad i < j,\\\\ &src_M(m_j) = dest_M(m_i), fire(m_i) = t_i, fire(m_j) = t_j\n",
    "\\end{align}\n",
    "\n",
    "`constraint forall (m in messages)(\n",
    "  starts[m] > if dependencies[m] > 0 then ends[messages[dependencies[m]]]+consume_time else 1 endif \n",
    ");`\n",
    "    "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "7. The seventh constraint defines the firing times for messages sent from a certain mapped actor to adhere to a chosen tdma rule for that PE node. Since the firing time $fire(m)$ denotes the time when message $m$ reaches the nearest router node (assumed one travelled edge for every PE in the NoC graph), we subtract one clock time.\n",
    "    $$\n",
    "\\begin{align}\n",
    "    fire(m) -1 = tdm^{max} \\times d_m + tdm_{(map_x(src_M(m)), map_y(src_M(m)))} \\quad \\forall m \\in \\mathcal{M}\n",
    "\\end{align}\n",
    "     $$\n",
    "\n",
    "`constraint forall (m in messages)(\n",
    "  (starts[m]-1) mod tdma_max = tdma_table[mapping(mapx[source(m)],mapy[source(m)]), mapping(mapx[dest(m)],mapy[dest(m)])]\n",
    ");\n",
    "`\n",
    "    "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "8. The eighth constraint guarantees that no two messages may be travelling across the same channel in the NoC at the same time, where either equations must be true. The same code is used for each of the four directions. Less than 2 means only one may be 1. Less than 3 means any value.\n",
    "$$\\begin{align}\n",
    "    &\\sum^{t} x_{t}^{m_i} + map_x(src_M(m_i)) = \\sum^{t} x_{t}^{m_j} + map_x(src_M(m_j)) \\label{eq1} \\\\\n",
    "    &\\text{ and}\\\\\n",
    "    &\\sum^{t} y_{t}^{m_i} + map_y(src_M(m_i))= \\sum^t y_{t}^{m_j} + map_y(src_M(m_j)) \\label{eq2}\n",
    "\\end{align}\n",
    "$$\n",
    "\n",
    "`constraint forall (t in 1..max_time-1)(\n",
    "  forall (m_prev in messages)(\n",
    "    forall (m_next in messages where m_next > m_prev)(\n",
    "      route_n[m_prev, t+1]+route_n[m_next, t+1] < if\n",
    "      mapping(mapx[source(m_prev)] + route_e[m_prev,t] - route_w[m_prev,t], mapy[source(m_prev)] + route_s[m_prev,t] - route_n[m_prev,t]) = mapping(mapx[source(m_next)] + route_e[m_next,t] - route_w[m_next,t], mapy[source(m_next)] + route_s[m_next,t] - route_n[m_next,t])\n",
    "      then 2\n",
    "      else 3\n",
    "      endif\n",
    "    )\n",
    "  )\n",
    ");`"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "9. The ninth constraint defines one and only one start time for each message. This does not need to be implemented in code since this applies by design.\n",
    "$$\n",
    "\\begin{align}\n",
    "    \\sum_{t\\in\\mathcal{T}} f_m^t = 1 \\quad \\forall m \\in \\mathcal{M}, fire(m)=t\n",
    "\\end{align}\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "10. The tenth constraint ensures that each actor takes up a unique position in the NoC. \n",
    "$$\n",
    "\\begin{align}\n",
    "    \\sum_a x_{ai} = |NoC_y| \\quad \\forall x_i \\in NoC_x, a \\in \\mathcal{A}\\\\\n",
    "    \\sum_a y_{ai} = |NoC_x| \\quad \\forall y_i \\in NoC_y, a \\in \\mathcal{A}\n",
    "\\end{align}\n",
    "$$\n",
    "     <code>constraint alldifferent([mapping(mapx[a],mapy[a]) | a in actors]);</code>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Model Output\n",
    "\n",
    "Model can be run by clicking on the app's run button, or on compile line (with additional data files [.dzn] as subsequent arguments) to generate values for the defined variables. Note that MiniZinc deal with multi-dimension arrays as flattened arrays so we need to manually divide the route list for each message. The first result applied TDMA, the second does not (hence odd value start times).\n",
    "\n",
    "![image](cp.png)\n",
    "![image](cp2.png)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
